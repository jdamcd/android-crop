apply plugin: 'android-library'
apply plugin: 'maven'

archivesBaseName = 'android-crop'

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.3'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19

        testPackageName 'com.soundcloud.android.crop.test'
        testInstrumentationRunner 'android.test.InstrumentationTestRunner'
    }
}

dependencies {
    compile 'org.easytesting:fest-util:1.2.5'
    androidTestCompile 'com.squareup:fest-android:1.0.7'
    androidTestCompile 'com.android.support:support-v4:19.0.+'
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.0'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.0'
}

task apklib(type: Zip) {
    dependsOn 'packageReleaseJar'
    destinationDir = file('build/libs')
    appendix = 'apklib'
    extension = 'apklib'

    from 'AndroidManifest.xml'
    into('res') {
        from 'res'
    }
    into('src') {
        from 'src'
    }
}

artifacts {
    archives apklib
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'http://maven.int.s-cloud.net/content/repositories/releases')

            modifyPom(addFilter('aar') { artifact, file ->
                artifact.name == archivesBaseName
            })

            modifyPom(addFilter('apklib') { artifact, file ->
                artifact.name == archivesBaseName + '-apklib'
            })
        }
    }
}

def modifyPom(pom) {
    pom.project {
        name 'android-crop'
        description 'A simple Activity for cropping images'
        url 'https://github.com/jdamcd/android-crop'

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        organization {
            name 'SoundCloud'
            url 'http://developers.soundcloud.com'
        }
    }
}
